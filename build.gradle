group 'uk.ac.ebi.subs'
version '0.2.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'org.hidetake.ssh'
apply plugin: 'application'

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.1.RELEASE")
        classpath 'org.hidetake:gradle-ssh-plugin:1.1.3'
    }
}

repositories {
        mavenLocal()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        mavenCentral()
}

dependencies {
    compileOnly("org.projectlombok:lombok:1.16.12")

    compile("uk.ac.ebi.subs:subs-processing-model:0.1.0-SNAPSHOT")
    compile("uk.ac.ebi.subs:subs-repository:0.1.0-SNAPSHOT")
    compile("uk.ac.ebi.subs:subs-data-model:0.1.0-SNAPSHOT")
    compile("uk.ac.ebi.subs:subs-messaging:1.0.3-SNAPSHOT")
    compile("uk.ac.ebi.subs:validator-common:0.1.0-SNAPSHOT")

    compile ('org.atteo:evo-inflector:1.2.1') // better pluralisation

    compile("org.springframework.boot:spring-boot-starter-data-mongodb")

    testCompile("junit:junit")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("uk.ac.ebi.subs:subs-test-categories:0.1.0-SNAPSHOT")

    compile 'org.springframework.security:spring-security-core'
}

task sourceJar(type: Jar) { from sourceSets.main.allJava }

artifacts {
    archives jar
    archives sourceJar {
        classifier "sources" 
        destinationDir = new File("build/libs/sources")
    }
}

uploadArchives {
    repositories {
        mavenDeployer {

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

            }

            pom.project {
                packaging 'jar'
                url 'https://github.com/EMBL-EBI-SUBS/subs-api-support'

                scm {
                    url 'https://github.com/EMBL-EBI-SUBS/subs-api-support.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'subs-internal'
                        name 'EMBL-EBI-SUBS'
                        email 'subs-internal@ebi.ac.uk'
                    }
                }
            }
        }
    }
}

ssh.settings {
    user = 'sub_adm'
}
 
if (project.hasProperty('sshKeyFile')) {
    ssh.settings.identity = new File(sshKeyFile)
}
 
if (project.hasProperty('sshPassphrase')) {
    ssh.settings.passphrase = sshPassphrase
}

task('externalCiTest', type: Test) {
    useJUnit {
        excludeCategories 'uk.ac.ebi.subs.MongoDBDependentTest'
    }
    testLogging {
        exceptionFormat = 'full'
    }
}

task printJarName {
    doLast {
        println "$jar.archivePath.name"
    }
 
}
 
task printJarFullPath {
    doLast {
        println "$jar.archivePath.absolutePath"
    }
}
 
remotes {
    staging {
        role('staging')
        host = 'mac-subs-002'
    }
}
 
task deployJar(type: SshTask, dependsOn: 'assemble') {
    doLast {
        def deployHome = "/data/" + project.name;
        File jarDeployDir = new File(deployHome,"jar")
 
        ssh.run {
            session(remotes.staging) {
                println "Creating $project.name directory in /data"
                execute "/data/create_dirs.sh $project.name", ignoreError: false
                println "Uploading jar $jar.archivePath.name to $jarDeployDir area on $remote"
                put(jar.archivePath.absolutePath, jarDeployDir.absolutePath)
                println "creating symbolic link to $jar.archiveName"
                def result = execute "ln -s -f $jarDeployDir/$jar.archiveName $jarDeployDir/$jar.baseName" + ".jar", ignoreError: true
                println result
                execute "chmod u+x $jarDeployDir/*", ignoreError: false 
            }
        }
    }
    ssh.settings {
        knownHosts = allowAnyHosts
    }
}

mainClassName = "uk.ac.ebi.subs.ApiSupportApplication"

springBoot {
    executable = true
}

